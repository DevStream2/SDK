plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.github.DevStream2'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    api 'org.apache.commons:commons-math3:3.6.1'
    implementation 'org.json:json:20231013'
    implementation 'com.google.guava:guava:30.1-jre'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'net.java.dev.jna:jna:5.12.1'
    testImplementation 'junit:junit:4.13.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

abstract class GenerateBuildConfigTask extends DefaultTask {
    @Input
    abstract Property<String> getBackendUrl()

    @OutputDirectory
    abstract DirectoryProperty getOutputDir()

    @TaskAction
    def generate() {
        def packageName = "org.Analytics"
        def buildConfigDir = outputDir.file("${packageName.replace('.', '/')}").get().asFile
        buildConfigDir.mkdirs()

        new File(buildConfigDir, "BuildConfig.java").text = """
package ${packageName};

public final class BuildConfig {
    public static final String BACKEND_BASE_URL = "${backendUrl.get()}";
}
"""
    }
}

tasks.register('generateBuildConfig', GenerateBuildConfigTask) {
    backendUrl = providers.gradleProperty('backendBaseUrl').orElse("http://192.168.1.216:3000")
    outputDir = layout.buildDirectory.dir("generated/sources/buildConfig/java/main")
}

compileJava {
    dependsOn tasks.named('generateBuildConfig')
}

sourceSets {
    main {
        java {
            srcDirs += tasks.named('generateBuildConfig').get().outputDir.get().asFile
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'com.github.DevStream2'
            artifactId = 'SDK'
            version = '1.0.0'
        }
    }
    repositories {
        maven {
            url = uri("$buildDir/repo")
        }
    }
}
